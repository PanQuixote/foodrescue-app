# ################# Lists to maintain manually #####################################################

# Application C++ source files to compile.
set(foodrescue_SRCS
    main.cpp
)

# Qt Resource Collection (.qrc) files.
#   (Files listed in .qrc files will be included into the executable.)
qt5_add_resources(RESOURCES
    resources.qrc
)

# Libraries to statically link into the Android executable.
#   (Desktop Linux systems will supply these system-wide as dynamic libraries.)
#   TODO: What happens on iOS, Windows and Mac OS X?
if (CMAKE_SYSTEM_NAME STREQUAL "Android")
    # Require the package that provides the Kirigami library we want to include.
    find_package(KF5Kirigami2 ${KF5_DEP_VERSION} REQUIRED)

    set(foodrescue_EXTRA_LIBS
        # FIXME: Static linking with QQC2 should not be needed, as it should be a dynamic
        # library coming in the APK. But if not listed here, the lib is omitted from the APK.
        Qt5::QuickControls2
        Qt5::AndroidExtras
        KF5::Kirigami2
        KF5::I18n # TODO: Remove if not using KDE i18n.
    )
endif()

# Breeze icons used in the application, to include into the Android APK.
#   (Desktop Linux systems will supply these system-wide for inclusion at run-time.)
#   TODO: What happens on iOS, Windows and Mac OS X?
#
#   To browse available icons to choose from, execute (as per https://askubuntu.com/a/1246673):
#   pcmanfm-qt "search:///usr/share/icons/breeze?recursive=1&name_regex=*&mime_types=image/*"
#
#   Also note that some Breeze icons are used by Kirigami components themselves and already
#   packaged. They can be re-used in the application without increasing file size. For
#   Kirigami 5.68.0, these icons are:
#     go-next go-previous go-up handle-sort overflow-menu-left overflow-menu-right
#     go-next-symbolic go-next-symbolic-rtl go-previous-symbolic go-previous-symbolic-rtl
#     overflow-menu user view-left-new view-right-new view-left-close view-right-close
#   Source: https://invent.kde.org/frameworks/kirigami/-/blob/f47bf90/KF5Kirigami2Macros.cmake#L15
if (CMAKE_SYSTEM_NAME STREQUAL "Android")
    # Require the package providing kirigami_package_breeze_icons().
    find_package(KF5Kirigami2 REQUIRED)

    kirigami_package_breeze_icons(ICONS
        configure
        dialog-warning
        help-about
    )
endif()


# ################# Compilation and installation ###################################################

# Define executable to generate, and its required inputs.
#   (Represented as "[hammer icon] foodrescue" in the Qt Creator project outline.)
add_executable(foodrescue ${foodrescue_SRCS} ${RESOURCES})

# Link all required libraries with the executable.
target_link_libraries(foodrescue
    # TODO: Why are some Qt libraries here and some in ${foodrescue_EXTRA_LIBS}? No Qt
    # library should need bundling for desktop targets, so all should be extra libs (only
    # included for Android builds), no?
    Qt5::Core
    Qt5::Quick
    Qt5::Qml
    Qt5::Svg
    ${foodrescue_EXTRA_LIBS}
)

# Install the binary generated by this makefile.
install(TARGETS foodrescue ${KF5_INSTALL_TARGETS_DEFAULT_ARGS})
